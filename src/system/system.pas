(*<system.pas>
 * Z80 and MSX related system routines.
 * CopyLeft (c) 1995-2024 by PopolonY2k.
 * CopyLeft (c) since 2024 by Hinotori Team.
 *)

(*
 * This module depends on folowing include files (respect the order):
 * - /system/systypes.pas;
 *)

(**
  * MSX version return codes.
  *)
const         ctMSX1    = 0;
              ctMSX2    = 1;
              ctMSX2P   = 2;
              ctMSXTR   = 3;
              ctMSXTRGT = 4;
              ctUnknown = 255;

(**
  * Switched I/O ports.
  *)
const         ctSwitchedIODevID  = $40;    { Device ID register  }
              ctSwitchedIOStatus = $41;    { Device status       }

(**
  * List of BIOS extension device ID's.
  *)
const         ctUnknownDevice    = 0;      { Not official        }
              ctASCIIMicrosoft   = 1;
              ctCanon            = 2;
              ctCasio            = 3;
              ctFujitsu          = 4;
              ctGeneral          = 5;
              ctHitachi          = 6;
              ctKyocera          = 7;
              ctMatsushita       = 8;      { Panasonic           }
              ctMitsubishi       = 9;
              ctNEC              = 10;
              ctNipponGakki      = 11;
              ctJVC              = 12;
              ctPhilips          = 13;
              ctPioneer          = 14;
              ctSanyo            = 15;
              ctSharp            = 16;
              ctSony             = 17;
              ctSpectravideo     = 18;
              ctToshiba          = 19;
              ctMitsumi          = 20;
              ctTelematica       = 21;
              ctGradiente        = 22;
              ctSharpBrazil      = 23;
              ctGoldStar         = 24;     { LG - Lucky GoldStar }
              ctDaewoo           = 25;
              ctSamsung          = 26;
              ctImageScanner     = 128;    { Matsushita          }
              ctOneChipZemmixNeo = 212;    { KDL Firmware        }
              ctMPS2             = 254;    { ASCII               }

(**
  * All CPU modes.
  *)
type TProcessorMode = ( ModeZ80,
                        ModeZ80PanaTurbo,
                        ModeZ80ZemmixTurbo,
                        ModeR800ROM,
                        ModeR800DRAM );



(**
  * Disable Z80 interrupts.
  *)
procedure DI;
begin
  InLine( $F3 );
end;

(**
  * Enable Z80 interrupts.
  *)
procedure EI;
begin
  InLine( $FB );
end;

(**
  * Get the MSX manufacturer.
  *)
function GetMSXManufacturer : byte;
var
      nDeviceId,
      nCount      : byte;

begin
  nCount := ctMPS2;

  while( nCount <> ctUnknownDevice ) do
  begin
    Port[ctSwitchedIODevID] := nCount;
    nDeviceId := not Port[ctSwitchedIODevID];

    if( nDeviceId = nCount )  then
      nCount := ctUnknownDevice
    else
    begin
      case nCount of
        ctMPS2             : nCount := ctOneChipZemmixNeo;
        ctOneChipZemmixNeo : nCount := ctImageScanner;
        ctImageScanner     : nCount := ctSamsung;
        else
        begin
          nCount    := Pred( nCount );
          nDeviceId := ctUnknownDevice;
        end;
      end;
    end;
  end;

  GetMSXManufacturer := nDeviceId;
end;

(**
  * Return the MSX version that's running.
  *)
function GetMSXVersion : byte;
var
    nModel,
    nMIDI   : byte;

begin
  (*
   * The ASM routine below is located in the .\ASM\ project folder
   * and was generated by INLASS.
   *)
  inline(
          $3A/$C1/$FC        { LD A,(EXPTBL)  }
          /$21/$2D/$00       { LD HL,MSXMODEL }
          /$CD/$0C/$00       { CALL RDSLT     }
          /$32/nModel        { LD (nModel),A  }
          /$3A/$C1/$FC       { LD A,(EXPTBL)  }
          /$21/$2E/$00       { LD HL,MSXMIDI  }
          /$CD/$0C/$00       { CALL RDSLT     }
          /$32/nMIDI         { LD (nMIDI),A   } );

  if( nMIDI = 1 )  then
    GetMSXVersion := ctMSXTRGT
  else
    GetMSXVersion := nModel;
end;

(**
  * Get the computer processor mode/type.
  *)
function GetProcessorMode : TProcessorMode;
var
       nMode  : byte;
       mode   : TProcessorMode;
begin
  if( GetMSXVersion in [ctMSXTR..ctMSXTRGT] )  then
  begin
    (*
     * The ASM routine below is located in the .\ASM\ project folder
     * and was generated by INLASS.
     *)
    inline(
            $DD/$21/$83/$01  { LD IX,GETCPU   }
            /$FD/$21/$00/$00 { LD IY,0        }
            /$CD/$1C/$00     { CALL CALSLT    }
            /$32/nMode       { LD (nMode),A   } );

    case nMode of
      1 : mode := ModeR800ROM;
      2 : mode := ModeR800DRAM;
    else
      mode := ModeZ80;
    end;
  end
  else
  begin
    mode := ModeZ80;

    (* Just consider the existing turbo machines *)
    case GetMSXManufacturer of
      ctOneChipZemmixNeo : if( ( Port[ctSwitchedIOStatus] and 1 ) = 0 )  then
                             mode := ModeZ80PanaTurbo
                           else  { TODO: Detect Z80Mode and Z80ZemmixTurbo }
                             mode := ModeZ80ZemmixTurbo;

      ctMatsushita       : if( ( Port[ctSwitchedIOStatus] and 1 ) = 0 )  then
                             mode := ModeZ80PanaTurbo
                           else
                             mode := ModeZ80;
    end;
  end;

  GetProcessorMode := mode;
end;

(**
  * Get the computer's frequency.
  *)
function GetHostFrequency : THostInterruptTiming;
var
       nRet  : byte;

begin
  (*
   * The ASM routine below is located in the .\ASM\ project folder
   * and was generated by INLASS.
   *)
  inline(
          $3A/$C1/$FC        { LD A,(EXPTBL)  }
          /$21/$2B/$00       { LD HL,SYSINFO  }
          /$CD/$0C/$00       { CALL RDSLT     }
          /$32/nRet          { LD (nRet),A    } );

  if( ( nRet shr 7 ) = 0 )  then
    GetHostFrequency := Timing60Hz
  else
    GetHostFrequency := Timing50Hz;
end;
